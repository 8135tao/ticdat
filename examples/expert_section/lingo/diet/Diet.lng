model:


! A diet problem in LINGO;
 sets:
! A SETS section describes the sets/data structures used;
  food: cost, x;
  category: min_nutrition, max_nutrition;
  fxc( food, category): qty;
 endsets

[OBJ] MIN = N;
 N = @sum( food( i): cost(i)*x(i));

 @for( category( j):
   [nutrilo] @sum( fxc( i, j): qty(i,j)*x(i)) >= min_nutrition( j);
   [nutrihi] @sum( fxc( i, j): qty(i,j)*x(i)) <= max_nutrition( j);
     );

 take test.lng
 data:
! Here we set up connections to the outside world;
! If we are introspective, we can get it directly...;
      food,   cost =
    hamburger 2.49
    salad 2.49
    hot_dog 1.50
    fries 1.89
    macaroni 2.09
    chicken 2.89
    milk 0.89
    ice_cream 1.59
    pizza 1.99;

    category     =   protein calories fat sodium;
    min_nutrition=   91 1800 0 0;
    max_nutrition=   10000000000 2200 65 1779;
          qty =      24 410 26 730
                     31 320 12 1230
                     20 560 32 1800
                     4 380 19 270
                     12 320 10 930
                     32 420 10 1190
                     8 100 2.5 125
                     8 330 10 180
                     15 320 12 820;
 enddata

DATA:
 @POINTER(1) = N;
 @POINTER(2) = X;
ENDDATA

end

